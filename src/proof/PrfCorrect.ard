\import Function.Meta
\import Logic
\import Meta
\import Paths
\import Paths.Meta
\import proof.Proof
\import semantics.Interpretation
\import syntax.Context
\import syntax.Substitution
\import syntax.Syntax
\open Interpretation

\func prfCorrectness {_ : Language} {c : Context} {axioms : Theory c} {toShow : Formula c}
                     (prf : Prf axioms toShow)
  : axioms Interpretation.⊧I toShow
\elim prf
  | AXM e => \lam _ m => m e
  | contra f prf1 prf2 => \lam I mAxioms => absurd $
  ⊧.contradict (prfCorrectness prf2 I mAxioms) (prfCorrectness prf1 I mAxioms)
  | introImp prf idp => \lam I mAxioms mA => prfCorrectness prf I (⊧A.fromAdd mAxioms mA)
  | refl idp idp => \lam _ _ => idp
  | notImp h1 h2 idp => \lam I mAxioms mImp =>
      \let
        | h1' => prfCorrectness h1 I mAxioms
        | h2' => prfCorrectness h2 I mAxioms
      \in h2' $ mImp $ h1'
  | notFalse idp => \lam _ _ mH => mH (\lam _ => idp)
  | notEqual h idp => \lam I _ evalEqual => h I evalEqual
  | forAllElim fa prfAll t prf => \lam I mAxioms =>
      \let
        | goal : I ⊧ Substitution.subst fa t =>
          \let rec => prfCorrectness prfAll I mAxioms
          \in ⊧.fromSub (rec (evaluate I t))
      \in prfCorrectness prf I (⊧A.fromAdd mAxioms goal)
  | notEqualSymm prf idp => \lam I mAxioms p => prfCorrectness prf I mAxioms (inv p)
  | notHFromimpH f' prf prf1 idp => \lam I mAxioms mF => prfCorrectness prf I mAxioms (prfCorrectness prf1 I mAxioms mF)
  | sub f t t' prf prf' idp => \lam I mAxioms =>
      \let
        | h1 => prfCorrectness prf I mAxioms
        | h2 => prfCorrectness prf' I mAxioms
        | h1' => Interpretation.models.fromSub.substitutionLemma-> I {extend I (evaluate I t)}
            (\case \elim __ \with {
              | 0 => idp
              | suc _ => idp
            })
            h1
        | h1'' => rewrite h2 in h1'
      \in Interpretation.models.fromSub.substitutionLemma<- I {extend I (evaluate I t')} (\case \elim __ \with {
        | 0 => idp
        | suc _ => idp
      }) h1''
  | symm prf idp => \lam I mAxioms => inv $ prfCorrectness prf I mAxioms
  | trans prf1 prf2 idp => \lam I mAxioms => prfCorrectness prf1 I mAxioms *> prfCorrectness prf2 I mAxioms
  | fcong {_} {_} {f} {args} {args'} prfs idp idp idp =>
    \let prfs' => prfCorrectnessArray (\lam i => equal (args i) (args' i)) prfs
    \in \lam I m => pmap (fun f) (ext (ext (\lam j => prfs' j I m)))
  | rcong {r} {args} {args'} prfs prf idp => \lam (I : Interpretation) m =>
      \let
        | prf' => prfCorrectness prf I m
        | prfs' => prfCorrectnessArray (\lam i => equal (args i) (args' i)) prfs
        | h :
          (\new Array (Universe {I.structure}) (relArity r) (\lam i => evaluate I (args i))) =
          (\new Array (Universe {I.structure}) (relArity r) (\lam i => evaluate I (args' i)))
        => ext (ext (\lam j => prfs' j I m))
      \in rewrite h prf'
  | and prf1 prf2 idp => \lam I m => (prfCorrectness prf1 I m, prfCorrectness prf2 I m)
  | modusPonens f prf1 prf2 => \lam I m => prfCorrectness prf2 I m (prfCorrectness prf1 I m)
  | cExistsIntro t prf idp => \lam I m =>
      \let
        h => prfCorrectness prf I m
      \in inP (
        Interpretation.evaluate I t,
        models.fromSub.substitutionLemma-> {_} {suc c} {c} {_} I {extend I (evaluate I t)} {Substitution.substOne t}
            (\lam (v : Fin (suc c)) => \case \elim v \with {
              | 0 => idp
              | suc f => idp
            })
            h
      )
  | cAndElim1 prf idp => \lam I m => \let rec => prfCorrectness prf I m \in rec.1
  | cAndElim2 prf idp => \lam I m => \let rec => prfCorrectness prf I m \in rec.2
  \where {
    -- to work around termintation checking

    \func prfCorrectnessArray {_ : Language} {c : Context} {axioms : Theory c} (toShow : Array (Formula c))
                              (prfs : DArray (\lam i => Prf axioms (toShow i)))
      : DArray (\lam i => axioms ⊧I toShow i)
    \elim toShow, prfs
      | nil, nil => nil
      | a :: l, p :: prfs => prfCorrectness p :: prfCorrectnessArray l prfs
  }