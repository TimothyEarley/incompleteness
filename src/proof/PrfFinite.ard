\import Function.Meta
\import Logic
\import Paths
\import Paths.Meta
\import Set
\import Set.Fin
\import Set.Subset
\import decidable.DecLanguage
\import decidable.FormulaCode
\import proof.PrfWeaken
\import proof.Proof
\import syntax.Context
\import syntax.Free
\import syntax.Lift
\import syntax.Substitution
\import syntax.Syntax
\import util.SetUtil

\func prfFinite {_ : DecLanguage} {c : Context} {axioms : Theory c} {toShow : Formula c}
                (prf : Prf axioms toShow) :
  \Sigma
    (axioms' : Theory c)
    (axioms' ⊆ axioms)
    (FinSet (Elements axioms'))
    (Prf axioms' toShow)
\elim prf
  | AXM e => (single toShow, SubsetHelper.subsetSingle e, FinSets.FinSingle, AXM idp)
  | contra f prf1 prf2 =>
    \let
      | h1 => prfFinite prf1
      | h2 => prfFinite prf2
    \in (
      h1.1 ∪ h2.1,
      SubsetHelper.subsetUnion h1.2 h2.2,
      FinSets.FinUnion h1.3 h2.3,
      contra f (prfWeaken h1.4 (\lam h => byLeft h)) (prfWeaken h2.4 (\lam h => byRight h))
    )
  | forAllElim f prfAll t prf =>
    \let
      | h' => prfFinite prfAll
      | h => prfFinite prf
      -- h.1 could have (subst f t) in it. Thats needs to be removed
      | axiomsH => delete h.1 (Substitution.subst f t)
      | axiomsHSub : axiomsH ⊆ axioms => \lam (e1, e2) => \case h.2 e1 \with {
        | byLeft a => a
        | byRight b => absurd $ e2 b
      }
      | axiomsHFin : FinSet (Elements axiomsH) => FinSets.FinWithout h.3 (decideEq _)
    \in (
      h'.1 ∪ axiomsH,
      SubsetHelper.subsetUnion h'.2 axiomsHSub,
      FinSets.FinUnion h'.3 axiomsHFin,
      forAllElim f (prfWeaken h'.4 (byLeft __)) t (prfWeaken h.4 (\lam {f'} e => \case decideEq (Substitution.subst f t) f' \with {
        | yes p => byRight p
        | no n => byLeft (byRight (e, n))
      }))
    )
  | introImp {a} prf p =>
    \let
      | h => prfFinite prf
      -- we could have used a, so remove it
    \in (
      delete h.1 a,
      SubsetHelper.subsetDelete h.2,
      FinSets.FinWithout h.3 (decideEq _),
      introImp (prfWeaken h.4 SubsetHelper.addDelete) p
    )
  | refl p p' => (∅, SubsetHelper.emptySubset, FinSets.FinEmpty, refl p p')
  | notImp prf1 prf2 p =>
    \let
      | h1 => prfFinite prf1
      | h2 => prfFinite prf2
    \in (
      h1.1 ∪ h2.1,
      SubsetHelper.subsetUnion h1.2 h2.2,
      FinSets.FinUnion h1.3 h2.3,
      notImp (prfWeaken h1.4 (byLeft __)) (prfWeaken h2.4 (byRight __)) p
    )
  | notFalse p => (∅, SubsetHelper.emptySubset, FinSets.FinEmpty, notFalse p)
  | notEqual h p => (∅, SubsetHelper.emptySubset, FinSets.FinEmpty, notEqual h p)
  | notEqualSymm prf p =>
    \let
      | h => prfFinite prf
    \in (h.1, h.2, h.3, notEqualSymm h.4 p)
  | notHFromimpH f' prf1 prf2 p =>
    \let
      | h1 => prfFinite prf1
      | h2 => prfFinite prf2
    \in (
      h1.1 ∪ h2.1,
      SubsetHelper.subsetUnion h1.2 h2.2,
      FinSets.FinUnion h1.3 h2.3,
      notHFromimpH f' (prfWeaken h1.4 (byLeft __)) (prfWeaken h2.4 (byRight __)) p
    )
  | sub f t t' prf1 prf2 idp =>
    \let
      | h1 => prfFinite prf1
      | h2 => prfFinite prf2
    \in (
      h1.1 ∪ h2.1,
      SubsetHelper.subsetUnion h1.2 h2.2,
      FinSets.FinUnion h1.3 h2.3,
      sub f t t' (prfWeaken h1.4 (byLeft __)) (prfWeaken h2.4 (byRight __)) idp
    )
  | symm prf p =>
    \let
      | h => prfFinite prf
    \in (h.1, h.2, h.3, symm h.4 p)
  | trans prf1 prf2 p =>
    \let
      | h1 => prfFinite prf1
      | h2 => prfFinite prf2
    \in (
      h1.1 ∪ h2.1,
      SubsetHelper.subsetUnion h1.2 h2.2,
      FinSets.FinUnion h1.3 h2.3,
      trans (prfWeaken h1.4 (byLeft __)) (prfWeaken h2.4 (byRight __)) p
    )
  | fcong {f} prfs idp idp p =>
    \let
      | prfs' => \lam i => prfFinite (prfs i)
      | S => \lam i => (prfs' i).1
    \in (
      finiteUnion S,
      SubsetHelper.subsetFiniteUnion {_} {S} (\lam i => (prfs' i).2),
      FinSets.FinFiniteUnion S (\lam i => (prfs' i).3),
      fcong (\lam i => prfWeaken (prfs' i).4 (SubsetHelper.subsetFiniteUnion' {_} {S} (i, idp))) idp idp p
    )
  | rcong prfs prf p =>
    \let
      | prf' => prfFinite prf
      | prfs' => \lam i => prfFinite (prfs i)
      | S => \lam i => (prfs' i).1
    \in (
      finiteUnion S ∪ prf'.1,
      SubsetHelper.subsetUnion (SubsetHelper.subsetFiniteUnion {_} {S} (\lam i => (prfs' i).2)) prf'.2 ,
      FinSets.FinUnion (FinSets.FinFiniteUnion S (\lam i => (prfs' i).3)) prf'.3,
      rcong
          (\lam i => prfWeaken (prfs' i).4 (\lam h => byLeft $ SubsetHelper.subsetFiniteUnion' {_} {S} (i, idp) h))
          (prfWeaken prf'.4 (byRight __)) p
    )
  | and prf1 prf2 p =>
    \let
      | h1 => prfFinite prf1
      | h2 => prfFinite prf2
    \in (
      h1.1 ∪ h2.1,
      SubsetHelper.subsetUnion h1.2 h2.2,
      FinSets.FinUnion h1.3 h2.3,
      and (prfWeaken h1.4 (byLeft __)) (prfWeaken h2.4 (byRight __)) p
    )
  | modusPonens f prf1 prf2 =>
    \let
      | h1 => prfFinite prf1
      | h2 => prfFinite prf2
    \in (
      h1.1 ∪ h2.1,
      SubsetHelper.subsetUnion h1.2 h2.2,
      FinSets.FinUnion h1.3 h2.3,
      modusPonens f (prfWeaken h1.4 (byLeft __)) (prfWeaken h2.4 (byRight __))
    )
  | cExistsIntro t prf p =>
    \let
      | h => prfFinite prf
    \in (h.1, h.2, h.3, cExistsIntro t h.4 p)
  | cAndElim1 prf p =>
    \let
      | h => prfFinite prf
    \in (h.1, h.2, h.3, cAndElim1 h.4 p)
  | cAndElim2 prf p =>
    \let
      | h => prfFinite prf
    \in (h.1, h.2, h.3, cAndElim2 h.4 p)
  | forAllIntro f prf p => rewrite p (forAllFinite prf)

  \where {
    \func forAllFinite {_ : DecLanguage} {c : Context} {axioms : Theory c} {f : Formula (suc c)}
                       (prf : Prf (liftTheory {_} {1} axioms) f) :
      \Sigma
        (axioms' : Theory c)
        (axioms' ⊆ axioms)
        (FinSet (Elements axioms'))
        (Prf axioms' (forAllH f)) =>
      \let
        | (h1, h2, h3, h4) => prfFinite prf
        | zeroNotFreeInH1 : 0 ∉ {Fin (suc c)} freeVars h1 => \case \elim __ \with {
          | inP (f, e, free) => \case h2 e \with {
            | inP (f', e', p') => notFreeInLift $ rewriteI p' $ free
          }
        }
        | zeroNotFreeInAnyH1 : \Pi (x : Formula (suc c)) (x ∈ h1) -> Not (FreeInFormula x 0)
        => \lam x e free => zeroNotFreeInH1 (inP (x, e, free))
        | ourSet : Theory c => mapSet h1
            (\lam x xe => liftFormula.unliftFormula1 x (zeroNotFreeInAnyH1 x xe))
        | ourSetLifted : liftTheory {_} {1} ourSet = {Theory (suc c)} h1 => liftUnlift zeroNotFreeInAnyH1
      \in (
        ourSet,
        \lam {x} o => \case \elim x, \elim o \with {
          | _, inP (x, e, idp) => \case h2 e \with {
            | inP (x', e', p') =>
              \let
                | p'' : x' = liftFormula.unliftFormula1 x (zeroNotFreeInAnyH1 x e) => inv $
                liftFormula.unliftFormula1Path p' notFreeInLift
                  *> liftFormula.unliftLift1
              \in rewriteI p'' e'
          }
        },
        FinSets.FinMap h3 _ (\lam _ _ _ => idp),
        forAllIntro f (rewrite ourSetLifted h4) idp
      )

    \func liftUnlift {_ : Language} {c : Context} {S : Theory (suc c)}
                     (h : \Pi (x : Formula (suc c)) (x ∈ S) -> Not (FreeInFormula x 0))
      : liftTheory {_} {1} (mapSet S (\lam x xe => liftFormula.unliftFormula1 x (h x xe))) = {Theory (suc c)} S =>
      ext (\lam f => ext (
        \lam l => \case \elim l \with {
          | inP (f', inP (x, xe, p'), p) => rewrite (p, p', liftFormula.liftUnlift1 (h _ _)) $ xe
        },
        \lam s => inP (liftFormula.unliftFormula1 f (h f s), inP (f, s, idp), inv $ liftFormula.liftUnlift1 (h f s))
      ))
  }